/*
 * Copyright 2023 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *  http://aws.amazon.com/apache2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

import com.github.spotbugs.snom.Effort

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "checkstyle"
    id "jacoco"
    id "com.github.spotbugs" version "6.0.8"
}

ext {
    // Load the plugin version from VERSION.
    libraryVersion = project.file("VERSION").getText('UTF-8').replace(System.lineSeparator(), "")
}

println "Smithy DocGen version: '${libraryVersion}'"

allprojects {
    group = "software.amazon.smithy.docgen"
    version = libraryVersion
}

subprojects {
    apply plugin: "java-library"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testImplementation("org.junit.jupiter:junit-jupiter-api:5.10.2")
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.10.2")
        testImplementation("org.junit.jupiter:junit-jupiter-params:5.10.2")
        testImplementation("org.hamcrest:hamcrest:2.2")
    }

    // Reusable license copySpec for building JARs
    def licenseSpec = copySpec {
        from "${project.rootDir}/LICENSE"
        from "${project.rootDir}/NOTICE"
    }

    // Set up tasks that build source and javadoc jars.
    task sourcesJar(type: Jar) {
        metaInf.with(licenseSpec)
        from {
            sourceSets.main.allJava
        }
        archiveClassifier = "sources"
    }

    // Build a javadoc JAR too.
    task javadocJar(type: Jar) {
        metaInf.with(licenseSpec)
        from {
            tasks.javadoc
        }
        archiveClassifier = "javadoc"
    }

    // Include an Automatic-Module-Name in all JARs.
    afterEvaluate { Project project ->
        tasks.jar {
            metaInf.with(licenseSpec)
            inputs.property("moduleName", project.ext["moduleName"])
            manifest {
                attributes "Automatic-Module-Name": project.ext["moduleName"]
            }
        }
    }

    // Always run javadoc after build.
    tasks["build"].dependsOn(tasks["javadoc"])

    // ==== Tests ====
    // https://docs.gradle.org/current/samples/sample_java_multi_project_with_junit5_tests.html
    test {
        useJUnitPlatform()
    }
    // Log on passed, skipped, and failed test events if the `-Plog-tests` property is set.
    // https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/testing/logging/TestLoggingContainer.html
    if (project.hasProperty("log-tests")) {
        test {
            testLogging {
                events = ["passed", "skipped", "failed"]
                exceptionFormat = "full"
            }
        }
    }

    // ==== Maven ====
    apply plugin: "maven-publish"
    apply plugin: "signing"

    publishing {
        publications {
            mavenJava(MavenPublication) { publication ->
                publication.from(components["java"])

                // Ship the source and javadoc jars.
                artifact(tasks["sourcesJar"])
                artifact(tasks["javadocJar"])

                // Include extra information in the POMs.
                project.afterEvaluate {
                    pom {
                        name.set(project.ext["displayName"].toString())
                        description.set(project.description)
                        url.set("https://github.com/smithy-lang/smithy-docgen")
                        licenses {
                            license {
                                name.set("Apache License 2.0")
                                url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                                distribution.set("repo")
                            }
                        }
                        developers {
                            developer {
                                id.set("smithy")
                                name.set("Smithy")
                                organization.set("Amazon Web Services")
                                organizationUrl.set("https://aws.amazon.com")
                                roles.add("developer")
                            }
                        }
                        scm {
                            url.set("https://github.com/smithy-lang/smithy-docgen.git")
                        }
                    }
                }
            }
        }
    }

    task copyMavenMetadataForDevelopment(type: Copy) {
        from('build/tmp/publishMavenJavaPublicationToMavenLocal') {
            rename 'module-maven-metadata.xml', 'maven-metadata.xml'
        }

        def wdir = System.getProperty("user.home") + '/.m2/repository/software/amazon/smithy/docgen/' + project.name
        into(wdir)
    }

    publishToMavenLocal.finalizedBy(copyMavenMetadataForDevelopment)

    // ==== CheckStyle ====
    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    apply plugin: "checkstyle"
    tasks["checkstyleTest"].enabled = false

    // ==== Code coverage ====
    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    apply plugin: "jacoco"
    // report is always generated after tests run
    test {
        finalizedBy jacocoTestReport
    }
    // tests are required to run before generating the report
    jacocoTestReport {
        dependsOn test
    }
    jacocoTestReport {
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('$buildDir/reports/jacoco')
        }
    }

    // ==== Spotbugs ====
    // https://plugins.gradle.org/plugin/com.github.spotbugs
    apply plugin: "com.github.spotbugs"
    // We don't need to lint tests.
    tasks["spotbugsTest"].enabled = false
    // Configure the bug filter for spotbugs.
    spotbugs {
        effort = Effort.MAX
        excludeFilter = file("${project.rootDir}/config/spotbugs/filter.xml")
    }
}

// The root project doesn't produce a JAR.
tasks["jar"].enabled = false
